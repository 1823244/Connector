Процедура ВыполнитьТестыНаСервере() Экспорт
	
	Тест_GetРезультатКакJson();
	Тест_GetРезультатКакДвоичныеДанные();
	Тест_GetРезультатКакПоток();
	Тест_GetРезультатКакСтрока();
	Тест_GetПроизвольныеЗаголовки();
	Тест_GetПараметры();
	Тест_GetУспешнийРедиректОтносительныйАдрес();
	Тест_GetУспешнийРедиректАбсолютныйАдрес();
	Тест_GetЗацикленныйРедирект();
	Тест_GetОтключенныйРедирект();
	
	Тест_Options();
	Тест_Head();
	Тест_PutПараметры();
	Тест_Delete();
	Тест_Patch();
	Тест_PostПараметры();
	
	Тест_Ошибка404();
	Тест_POST_x_www_form_urlencoded();
	Тест_POST_json();
	Тест_POST_MultipartFormData();
	
	Тест_Таймаут();
	
	//Тест_ОтправитьCookies();
	//
	//Тест_Session();
	//Тест_SetCookies();
	//Tect_Magic();
	//
КонецПроцедуры

#Область Тесты

Процедура Тест_GetРезультатКакJson()
	
	Результат = КоннекторHTTP.КакJson(КоннекторHTTP.Get("http://httpbin.org/get"));
	
	УтверждениеВерно(Результат["url"], "http://httpbin.org/get");
	
КонецПроцедуры

Процедура Тест_GetРезультатКакДвоичныеДанные()
	
	Результат = КоннекторHTTP.КакДвоичныеДанные(КоннекторHTTP.Get("http://httpbin.org/image/png"), Ложь);
	
	УтверждениеВерно(ТипЗнч(Результат), Тип("ДвоичныеДанные"));
	УтверждениеВерно(ПосчитатьMD5(Результат), "5cca6069f68fbf739fce37e0963f21e7");
	
КонецПроцедуры

Процедура Тест_GetРезультатКакПоток()
	
	Результат = КоннекторHTTP.КакДвоичныеДанные(КоннекторHTTP.Get("http://httpbin.org/image/png"), Истина);
	
	УтверждениеВерно(ТипЗнч(Результат), Тип("Поток"));
	УтверждениеВерно(ПосчитатьMD5(Результат), "5cca6069f68fbf739fce37e0963f21e7");
	
КонецПроцедуры

Процедура Тест_GetРезультатКакСтрока()
	
	Результат = КоннекторHTTP.КакТекст(КоннекторHTTP.Get("http://httpbin.org/encoding/utf8"));
	
	УтверждениеВерно(СтрНайти(Результат, "Зарегистрируйтесь сейчас на Десятую Международную"), 3931);
	
КонецПроцедуры

Процедура Тест_GetПроизвольныеЗаголовки()
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-My-Header", "Hello!!!");
	Результат = КоннекторHTTP.КакJson(КоннекторHTTP.Get("http://httpbin.org/headers",, Новый Структура("Заголовки", Заголовки)));
	
	УтверждениеВерно(Результат["headers"]["X-My-Header"], "Hello!!!");
	
КонецПроцедуры

Процедура Тест_GetПараметры()
	
	ПараметрыЗапроса = Новый Структура("key1, key2", "value1", "значение2");	
	Результат = КоннекторHTTP.КакJson(КоннекторHTTP.Get("http://httpbin.org/get", ПараметрыЗапроса));
	
	УтверждениеВерно(Результат["args"]["key1"], "value1");
	УтверждениеВерно(Результат["args"]["key2"], "значение2");
	
КонецПроцедуры

Процедура Тест_GetУспешнийРедиректОтносительныйАдрес()
	
	Ответ = КоннекторHTTP.Get("http://httpbin.org/relative-redirect/6");
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	УтверждениеВерно(Результат["url"], "http://httpbin.org/get");
	
КонецПроцедуры

Процедура Тест_GetУспешнийРедиректАбсолютныйАдрес()
	
	Ответ = КоннекторHTTP.Get("http://httpbin.org/absolute-redirect/6");
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	УтверждениеВерно(Результат["url"], "http://httpbin.org/get");
	
КонецПроцедуры

Процедура Тест_GetЗацикленныйРедирект()
	
	Попытка
		Результат = КоннекторHTTP.КакJson(КоннекторHTTP.Get("http://httpbin.org/redirect/31"));
	Исключение
		ВерноеИсключение(ИнформацияОбОшибке(), "СлишкомМногоПеренаправлений");
	КонецПопытки;
	
КонецПроцедуры

Процедура Тест_GetОтключенныйРедирект()
	
	Параметры = Новый Структура("РазрешитьПеренаправление", Ложь);	
	Ответ = КоннекторHTTP.Get("http://httpbin.org/redirect-to?url=http%3A%2F%2Fhttpbin.org%2Fget&status_code=307",, Параметры);
	
	УтверждениеВерно(Ответ.КодСостояния, 307);
	
КонецПроцедуры

Процедура Тест_Options()
	
	Ответ = КоннекторHTTP.Options("http://httpbin.org/anything");
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_Head()
	
	Ответ = КоннекторHTTP.Head("http://httpbin.org/anything");
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_Delete()
	
	Ответ = КоннекторHTTP.Delete("http://httpbin.org/delete");
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_Patch()
	
	Ответ = КоннекторHTTP.Patch("http://httpbin.org/patch");
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	
КонецПроцедуры

Процедура Тест_PutПараметры()
	
	Ответ = КоннекторHTTP.Put("http://httpbin.org/put",, Новый Структура("ПараметрыЗапроса", Новый Структура("key1, key2", "value1", "значение2")));
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	УтверждениеВерно(Ответ.URL, "http://httpbin.org/put?key1=value1&key2=%D0%B7%D0%BD%D0%B0%D1%87%D0%B5%D0%BD%D0%B8%D0%B52");
	
КонецПроцедуры

Процедура Тест_PostПараметры()
	
	Ответ = КоннекторHTTP.Post("http://httpbin.org/post",,, Новый Структура("ПараметрыЗапроса", Новый Структура("key1, key2", "value1", "значение2")));
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Ответ.КодСостояния, 200);
	УтверждениеВерно(Ответ.URL, "http://httpbin.org/post?key1=value1&key2=%D0%B7%D0%BD%D0%B0%D1%87%D0%B5%D0%BD%D0%B8%D0%B52");
	
КонецПроцедуры

Процедура Тест_Ошибка404()
	
	Ответ = КоннекторHTTP.Get("http://httpbin.org/status/404");
	
	УтверждениеВерно(Ответ.КодСостояния, 404);
	
КонецПроцедуры

Процедура Тест_POST_x_www_form_urlencoded()
	
	Данные = Новый Структура;
	Данные.Вставить("comments", "Постучать в дверь");
	Данные.Вставить("custemail", "vasya@mail.ru");
	Данные.Вставить("custname", "Вася");
	Данные.Вставить("custtel", "112");
	Данные.Вставить("delivery", "20:20");
	Данные.Вставить("size", "medium");
	Данные.Вставить("topping", СтрРазделить("bacon,mushroom", ","));
	
	Ответ = КоннекторHTTP.Post("http://httpbin.org/post", Данные);
	УтверждениеВерно(Ответ.URL, "http://httpbin.org/post");
	Результат = КоннекторHTTP.КакJson(Ответ);
	УтверждениеВерно(Результат["form"]["size"], "medium");
	УтверждениеВерно(Результат["form"]["comments"], "Постучать в дверь");
	УтверждениеВерно(Результат["form"]["custemail"], "vasya@mail.ru");
	УтверждениеВерно(Результат["form"]["delivery"], "20:20");
	УтверждениеВерно(Результат["form"]["custtel"], "112");	
	УтверждениеВерно(Ответ.Запрос.Заголовки.Получить("Content-Type"), "application/x-www-form-urlencoded");	
	
КонецПроцедуры

Процедура Тест_POST_json()
	
	Json = Новый Структура;
	Json.Вставить("key1", "value1");
	Json.Вставить("key2", "значение2");
	
	Ответ = КоннекторHTTP.Post("http://httpbin.org/post",, Json);
	Результат = КоннекторHTTP.КакJson(Ответ);
	УтверждениеВерно(Результат["json"]["key1"], "value1");
	УтверждениеВерно(Результат["json"]["key2"], "значение2");
	УтверждениеВерно(Ответ.Запрос.Заголовки.Получить("Content-Type"), "application/json");	

КонецПроцедуры

Процедура Тест_POST_MultipartFormData()
	
		
	Файлы = Новый Структура;
	Файлы.Вставить("Имя", "test.txt");
	Файлы.Вставить("Файл", );
	Файлы.Вставить("Тип", );
	Файлы.Вставить("Заголовки", );
	
//TODO:	
//	>>> url = 'https://httpbin.org/post'
//>>> multiple_files = [
//        ('images', ('foo.png', open('foo.png', 'rb'), 'image/png')),
//        ('images', ('bar.png', open('bar.png', 'rb'), 'image/png'))]
//>>> r = requests.post(url, files=multiple_files)
//>>> r.text
//{
//  ...
//  'files': {'images': 'data:image/png;base64,iVBORw ....'}
//  'Content-Type': 'multipart/form-data; boundary=3131623adb2043caaeb5538cc7aa0b3a',
//  ...
//}


	
	Ответ = КоннекторHTTP.КакJson(КоннекторHTTP.Get("http://httpbin.org/gzip"));
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Accept-Encoding", "gzip");
	Ответ = 
		КоннекторHTTP.Get(
			"https://raw.githubusercontent.com/dscape/spell/master/test/resources/big.txt", 
			,
			Новый Структура("Заголовки", Заголовки));
	
	

КонецПроцедуры

Процедура Тест_Таймаут()

	Попытка
		Ответ = КоннекторHTTP.Get("https://httpbin.org/delay/10",, Новый Структура("Таймаут", 1));
	Исключение
		ВерноеИсключение(ИнформацияОбОшибке(), "Превышено время ожидания");
	КонецПопытки;
	
КонецПроцедуры

Процедура Тест_ОтправитьCookies()
	
	Cookies = Новый Соответствие;
	Cookies.Вставить("k1", Строка(Новый УникальныйИдентификатор));
	Cookies.Вставить("k2", Строка(Новый УникальныйИдентификатор));
	Ответ = КоннекторHTTP.Get("http://httpbin.org/cookies",, Новый Структура("Cookies", Cookies));
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	УтверждениеВерно(Результат["k1"], Cookies["k1"]);
	УтверждениеВерно(Результат["k2"], Cookies["k2"]);
	
КонецПроцедуры

Процедура Тест_Session()
	
	Сеанс = КоннекторHTTP.СоздатьСеанс();
	Ответ = КоннекторHTTP.ВызватьМетодВСеансе(Сеанс, "GET", "https://releases.1c.ru/total");
	
	Данные = Новый Структура;
	Данные.Вставить("execution", ИзвлечьExecution(Ответ));
	Данные.Вставить("username", Константы.Логин.Получить()); 	
	Данные.Вставить("password", Константы.Пароль.Получить());
	Данные.Вставить("_eventId", "submit");
	Данные.Вставить("geolocation", "");
	Данные.Вставить("submit", "Войти");
	Данные.Вставить("rememberMe", "on");
	
	Ответ = КоннекторHTTP.ВызватьМетодВСеансе(Сеанс, "POST", "https://login.1c.ru/login", Новый Структура("Данные", Данные));
		
КонецПроцедуры

Процедура Тест_SetCookies()
	
	Ответ = КоннекторHTTP.Get("http://httpbin.org/cookies/set?k2=v2&k1=v1");
	
КонецПроцедуры

Процедура TODO_тест_сессии()
	
	Результат = КоннекторHTTP.КакJson(КоннекторHTTP.Get("https://api.github.com/events"));
	
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Новый СертификатыУдостоверяющихЦентровОС);
	Соединение = Новый HTTPСоединение("api.github.com", 443,,,, 30, ЗащищенноеСоединение);	
	Запрос = Новый HTTPЗапрос("/events");
	Ответ = Соединение.Получить(Запрос);
	Поток = Ответ.ИсходныйОтвет.ПолучитьТелоКакПоток();
	Кодировка = "utf-8"; // ну допустим мы знаем что там такая кодировка
	
	Ридер = Новый ЧтениеJSON;
	Ридер.ОткрытьПоток(Поток, Кодировка); // Кодировка в заголовке ответа
	Объект = ПрочитатьJSON(Ридер);
	Результат = Ридер.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИзвлечьExecution(Ответ)
	
	// TODO: Доделать
	//def extract_execution(r):
	//if r.status_code == 200:
	//    html = r.text
	//    i = html.find('name="execution"')
	//    if i > 0:
	//        j = html.find('>', i)
	//    new_str = html[i:j]
	//    b = new_str.find('value="') + len('value="')
	//    e = new_str.find('"', b)

	//    execution = new_str[b:e]
	//return execution
	//
КонецФункции

Функция ПосчитатьMD5(Данные)
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.MD5);
	Хеширование.Добавить(Данные);
	
	Возврат НРег(ПолучитьHexСтрокуИзДвоичныхДанных(Хеширование.ХешСумма));
	
КонецФункции

Процедура УтверждениеВерно(ЛевоеЗначение, ПравоеЗначение, Пояснение = "")
	
	Если ЛевоеЗначение <> ПравоеЗначение Тогда
		ВызватьИсключение(СтрШаблон("<%1> не равно <%2>: %3", ЛевоеЗначение, ПравоеЗначение, Пояснение));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВерноеИсключение(ИнформацияОбОшибке, ОжидаемоеИсключение)
	
	Если Не СтрНайти(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), ОжидаемоеИсключение) Тогда
		ВызватьИсключение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти